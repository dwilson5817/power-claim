plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'kr.entree.spigradle' version '2.3.3'
    id 'maven-publish'
    id 'java'
}

apply from: 'version.gradle'

repositories {
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'

    // https://mvnrepository.com/artifact/com.zaxxer/HikariCP
    implementation 'com.zaxxer:HikariCP:4.0.3'

    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation 'commons-io:commons-io:2.6'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "${publishUrl}/packages/maven"
            name "GitLab"
            credentials(HttpHeaderCredentials) {
                name = 'Job-Token'
                value = System.getenv("CI_JOB_TOKEN")
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}

spigot {
    commands {
        claim {
            description = 'Claim the chunk you are standing in'
            permission = 'powerclaim.claim'
            usage = '/<command>'
        }
        unclaim {
            description = 'Unclaim the chunk you are standing in'
            permission = 'powerclaim.unclaim'
            usage = '/<command>'
        }
    }
    permissions {
        'powerclaim.claim' {
            description = 'Allows claim command'
        }
        'powerclaim.unclaim' {
            description = 'Allows unclaim command'
        }
    }
}

test {
    useJUnitPlatform()
}